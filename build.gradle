plugins {
    id "java"

    id "org.springframework.boot" version "2.3.2.RELEASE"
    id "io.spring.dependency-management" version "1.0.10.RELEASE"
}

ext {
    versions = ext {
        embeddedDatabaseSpringTestVersion = "1.5.5"
        flywaySpringTestVersion = "6.4.0"
    }
}

allprojects {
    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.release = 11
        options.encoding = "UTF-8"
    }

    // plugin: io.spring.dependency-management
    dependencyManagement {
        imports {
            mavenBom("org.jdbi:jdbi3-bom:3.14.1")

            // Override Embedded PostgreSQL version for https://github.com/zonkyio/embedded-postgres.
            mavenBom("io.zonky.test.postgres:embedded-postgres-binaries-bom:12.3.0")
        }
    }
}

// plugin: org.springframework.boot
bootJar {
    mainClassName = "spring.boot.embedded.postgres.example.App"
    classifier = "boot"
}

dependencies {
    developmentOnly(group: "org.springframework.boot", name: "spring-boot-devtools")

    implementation(group: "org.springframework.boot", name: "spring-boot-starter")
    implementation(group: "org.springframework.boot", name: "spring-boot-starter-jdbc")

    implementation(group: "org.postgresql", name: "postgresql")
    implementation(group: "org.jdbi", name: "jdbi3-postgres")
    // Integrated support for JDBI and Spring 4. Spring 5 is also supported.
    implementation(group: "org.jdbi", name: "jdbi3-spring4")

    testImplementation(group: "org.springframework.boot", name: "spring-boot-starter-test") {
        exclude(group: "org.junit.vintage", module: "junit-vintage-engine")
    }

    testImplementation(group: "io.zonky.test", name: "embedded-database-spring-test", version: "${versions.embeddedDatabaseSpringTestVersion}") {
        exclude(group: "org.flywaydb.flyway-test-extensions", module: "flyway-spring-test")
    }
    testImplementation(group: "org.flywaydb.flyway-test-extensions", name: "flyway-spring-test", version: "${versions.flywaySpringTestVersion}")
}

test {
    useJUnitPlatform()
}
